# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-sdl2
pkgver=2.0.12
pkgrel=3
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo Switch port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=('switch-mesa' 'libnx')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-${pkgver}.patch")
groups=('switch-portlibs' 'switch-sdl2')
sha256sums=(
  '349268f695c02efbc9b9148a70b85e58cefbbf704abd3e91be654db7f1e2c863'
  'b5bf129b9218f04632babbb32b4f74c2167df369646b94c5ff7e1aed4c38df85'
)

build() {
  cd SDL2-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -p1 -i $srcdir/SDL2-${pkgver}.patch

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
  --disable-shared --enable-static \
  --enable-audio \
  --enable-joystick \
  --disable-power \
  --disable-filesystem \
  --enable-cpuinfo \
  --enable-threads \
  --enable-timers \
  --enable-video

  make
}

package() {
  cd SDL2-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

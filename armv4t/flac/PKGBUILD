# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>
# Contributor: oreo639 <oreo6391@gmail.com>

pkgname=dkp-armv4t-flac
pkgver=1.3.2
pkgrel=1
pkgdesc='Open source Free Lossless Audio Codec'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
depends=("dkp-armv4t-libogg")
makedepends=('devkitpro-pkgbuild-helpers')
groups=('dkp-armv4t-portlibs')

source=(
  "https://ftp.osuosl.org/pub/xiph/releases/flac/flac-$pkgver.tar.xz"
  "flac-${pkgver}.patch"
)
sha256sums=(
  '91cfc3ed61dc40f47f050a109b08610667d73477af6ef36dcad31c31a4a8d53f'
  '1687115411727bbe6d9323a4b0702df4fb29c35b2fe845534c3793623a88ae36'
)

build() {
  cd flac-$pkgver

  source /opt/devkitpro/armv4tvars.sh

  patch -Np1 -i "$srcdir/flac-${pkgver}.patch"

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static --disable-examples \
    --disable-xmms-plugin --disable-doxygen-docs \
    --disable-sse

  make -C src/libFLAC
}

package() {
  cd flac-$pkgver

  make DESTDIR="$pkgdir" install
}

